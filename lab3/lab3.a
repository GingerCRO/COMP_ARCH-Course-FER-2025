init    lui s0, %hi(0xFFFF0F00)
        addi s0, s0, %lo(0xFFFF0F00)            ; adresa od GPIO1 (display)

        lui x4, %hi(0xFFFF0B00)
        addi x4, x4, %lo(0xFFFF0B00)            ; adresa od GPIO2 (tipka i sklopka)

        addi x5, x0, 0b11100000                 ; XPA0 = 0 (ULAZ – tipka), XPA1 = 0 (ULAZ – sklopka)
        sw x5, 8(x4)                            ; spremi smjer u PA_DDR

        addi x5, x0, 0b00000000                 ; zadavanje smjera za display --> 0 = izlaz
        sw x5, 8(s0)                            ; spremanje smjera u PB_DDR

        addi x17, x0, 0                         ; inicijalizacija counter = 0

sklopka lw x5, 0(x4)                            ; citanje stanja sklopke
        andi x15, x5, 2                         ; ispitaj bit 1 --> je li sklopka aktivna
        beq x15, x0, sklopka                    ; ako nije aktivna --> skoci ponovo na labelu sklopka

        andi x15, x5, 1                         ; ispitaj bit 0 --> je li tipka kliknuta
        beq x15, x0, sklopka                    ; ako tipka nije kliknuta, ne radi nista
        
        addi x17, x17, 1                        ; ako je tipka kliknuta, povecaj counter

obradi  sltiu x15, x17, 15                      ; je li x17 < 15 (ako je istina --> x15 = 1, inace x15 = 0)
        bne x15, x0, nastavi                    ; ako je x17 < 15 nastavi bez resetiranja
        addi x17, x0, 1                         ; inače, resetiraj counter na 1
nastavi sltiu x15, x17, 10                      ; je li counter < 10
        bne x15, x0, jednaZn                    ; ako je counter < 10 --> skoči na labelu jednaZn
        addi x10, x0, 49                        ; ascii oznaka broja 1 (desetica)
        addi x11, x17, -10                      
        addi x11, x11, 48                       ; ascii oznaka znamenke jedinice
        
        addi x7, x0, 0x0D                       ; spremi CR oznaku u x7
        jal ra, lcdwr                           ; očisti zapis na displayu 
        addi x7, x10, 0                         ; spremi znamenku desetice u x7
        jal ra, lcdwr                           ; pošalji znamenku desetice na display
        addi x7, x11, 0                         ; spremi znamenku jedinice u x7
        jal ra, lcdwr                           ; pošalji znamenku jedinice na display
        addi x7, x0, 0x0A                       ; spremi LF oznaku u x7 
        jal ra, lcdwr                           ; pošalji znamenke na display
        jal x0, sklopka                         ; vrati se na labelu sklopka

jednaZn addi x10, x0, 0                         ; spremanje desetica = 0 u x10
        addi x11, x17, 48                       ; spremanje jedinice u x11
        addi x7, x0, 0x0D                       ; spremi CR oznaku u x7
        jal ra, lcdwr                           ; očisti display
        addi x7, x11, 0                         ; spremi znamenku jedinice u x7
        jal ra, lcdwr                           ; pošalji znamenku jedinice na display
        addi x7, x0, 0x0A                       ; spremi LF oznaku u x7
        jal ra, lcdwr                           ; pošalji znamenku jedinice na display
        jal x0, sklopka                         ; vrati se na labelu sklopka

lcdwr   andi x7, x7, 0x7F                       ; postavi bit 7 na nulu (tu je možda mogla biti jedinica)
        sb x7, 4(s0)                            ; pošalji znak
        ori x7, x7, 0x80                        ; postavi bit 7 u jedan (podigni impuls)
        sb x7, 4(s0)                            ; pošalji znak
        andi x7, x7, 0x7F                       ; postavi bit 7 u nulu (spusti impuls)
        sb x7, 4(s0)                            ; pošalji znak
        jalr x0, 0(ra)                          ; povratak